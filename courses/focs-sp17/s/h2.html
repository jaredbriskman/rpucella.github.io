<html>

<body>
<pre>

   (* Q1: Set operations *)

let rec inS e xs = 
  match xs with 
  | [] -> false
  | x::xs' -> (if x = e then true else inS e xs')

let rec subsetS xs ys = 
  match xs with 
  | [] -> true
  | x::xs' -> inS x ys && subsetS xs' ys

let rec equalS xs ys = 
  subsetS xs ys && subsetS ys xs

let rec unionS xs ys = 
  xs @ ys

let rec interS xs ys = 
  match xs with
  | [] -> []
  | x::xs' -> if inS x ys then x::interS xs' ys else interS xs' ys

let rec sizeS xs = 
  (* return the number of distinct elements! *)
  match xs with 
    [] -> 0
  | x::xs' -> (if inS x xs' then sizeS xs'
	       else 1 + sizeS xs')



   (* Q2: Language operations *)

let rec atMost n xs = 
   match xs with
     [] -> []
   | (x::xs) -> if String.length x > n then atMost n xs
                else x::(atMost n xs)


let rec unionL n xs ys = 
   match xs with
     [] -> atMost n ys
   | x::xs -> if String.length x > n then unionL n xs ys
              else x::unionL n xs ys


let rec concat_string_to_lang n x ys = 
   match ys with
     [] -> []
   | y::ys -> let s = x^y in 
              if String.length s > n then concat_string_to_lang n x ys
              else s::concat_string_to_lang n x ys


let rec concatL n xs ys = 
   match xs with
     [] -> []
   | x::xs -> (concat_string_to_lang n x ys)@(concatL n xs ys)


(* This uses the observation that all strings of length up to k
 * in A^* are in A^0 U A^1 U A^2 U ... U A^k
 *)

let rec starL n xs = 
   let rec loop n xs iter = 
     if iter = 0 then [""]
     else if iter = 1 then atMost n xs
     else unionL n xs (concatL n xs (loop n xs (iter -1)))  in
   ""::(loop n xs n)





   (* Q3: regular expressions *)

type re = 
    Empty 
  | Unit 
  | Letter of string 
  | Plus of re * re 
  | Times of re * re 
  | Star of re

let lang n s = 
  let fromChar c = String.make 1 c in
  let explode s = 
    let rec loop i result = 
      if i &lt; 0 then result
      else loop (i-1) (s.[i]::result) in
    loop (String.length s - 1) []  in
  let isalpha = function 'A'..'Z'|'a'..'z' -> true | _ -> false in
  let expect c cs = 
    match cs with 
      f::cs when f = c -> Some cs
    | _ -> None in
  let expect_alpha cs = 
    match cs with
      f::cs when isalpha f -> Some (f,cs)
    | _ -> None  in
  let rec parse_R cs = 
    match parse_R1 cs with
      None -> None
    | Some (r1,cs) -> 
        (match expect '+' cs with
           None -> Some (r1,cs)
         | Some cs -> 
             (match parse_R cs with
                None -> None
              | Some (r2,cs) -> Some (Plus(r1,r2),cs)))
  and parse_R1 cs = 
    match parse_R2 cs with
      None -> None
    | Some (r1,cs) -> 
        (match parse_R1 cs with
           None -> Some (r1,cs)
         | Some (r2,cs) -> Some (Times(r1,r2),cs))  
  and parse_R2 cs = 
    match parse_R3 cs with
      None -> None
    | Some (r1,cs) -> 
        (match expect '*' cs with
           None -> Some (r1,cs)
         | Some cs -> Some (Star(r1),cs))
  and parse_R3 cs = 
    match expect_alpha cs with
      Some (a,cs) -> Some (Letter(fromChar(a)),cs)
    | None -> 
        (match expect '1' cs with
           Some cs -> Some (Unit, cs)
         | None -> 
             (match expect '0' cs with
                Some cs -> Some (Empty,cs)
              | None -> parse_parens cs))
  and parse_parens cs = 
    match expect '(' cs with
      None -> None
    | Some cs -> 
        (match parse_R cs with
           None -> None
         | Some (r,cs) -> 
             (match expect ')' cs with
                None -> None
              | Some cs -> Some (r,cs)))  in
  let parse s = 
    let cs = explode s in
    match parse_R cs with
      Some (re,[]) -> re
    | _ -> failwith ("Cannot parse "^s)  in
  let rec eval re = 
    match re with
      Empty -> []
    | Unit -> [""]
    | Letter (a) -> [a]
    | Plus (r1,r2) -> unionL n (eval r1) (eval r2)
    | Times (r1,r2) -> concatL n (eval r1) (eval r2)
    | Star r -> starL n (eval r)  in
  eval (parse s)


let show l = 
  let rec loop l seen = 
    match l with
    | [] -> ()
    | s::rest -> if List.mem s seen 
                    then loop rest seen 
                  else (match s with 
		        | "" -> (print_string "  &lt;empty string>\n"; loop rest (""::seen))
			| s -> (print_string ("  "^s^"\n"); loop rest (s::seen))) in
  loop l []


(* QUESTION 3 *)

let regexp_a = "(d+e)(d+e)(d+e)(d+e)"
let regexp_b = "(d+e)((d+e)(d+e))*"
let regexp_c = "e*de*de*"
let regexp_d = "e*de*(de*de*)*"
let regexp_e = "e*(deee*)*"

</pre>
</body>

</html>
